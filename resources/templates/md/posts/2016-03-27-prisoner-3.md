{:title "Prisoner’s Dilemma, part 3" 
 :layout :post 
 :tags ["clojurescript" "lazy" "doall" "prisoner’s dilemma" "nginx" "validation"]}

I have a working version of the Prisoner’s Dilemma app. It’s online [here](https://flashbangsplat.com/prisoner). The code is [here](https://github.com/ethan826/prisoners-dilemma).

### Problem with lazy evaluation in `map`

Once I played more than about 5,400 rounds, I got a stack overflow. It turns out the problem was this line of code in the scorekeeping function:

    (defn get-score [results]
      (reduce #(map + (score-game %2) %)) [0 0] results)
  
The problem, as I understand it, is that the map function evaluates lazily, so rather each iteration of `reduce` doing the simple arithmetic with the score and modifying the accumulator, it instead accumulated a zillion calls to the map function, causing the stack overflow.

    (defn get-score [results]
      (reduce #(doall (map + (score-game %2) %)) [0 0] results))
      
Works because `doall` forces each call to map to evaluate eagerly, meaning that the arithmetic completes then and there without a bunch of thunks (I think that’s what the term is) accumulating.

### Problem with routing

A pretty subtle mistake was leading to a page not found error. In nginx, the server block had this:

    location /prisoner {
            proxy_pass http://localhost:8082;
    }

That means that a request to `https://flashbangsplat.com/prisoner` was being proxied not to `/` in the prisoner’s dilemma app, but instead to `/prisoner`, for which no route existed. By a simple adjustment (notice the extra `/`s), I got it working. The extra solidus signals nginx to strip out the route it receives before proxying it to the other server.


    location /prisoner/ {
            proxy_pass http://localhost:8082/;
    }

### Validation

Even if you set the `type` in a form to `number`, I found out you can still create invalid information by using `e`. You can input `e` so that you can do 1e3 (to make 1000), but you can actually type `e500`, which is not a valid number. So I came up with this:

Within the template:


    [bind-fields
      my-form
      doc
      (fn [id value local-copy-of-doc]
        (enforce-number id value local-copy-of-doc doc))] ; Keep helper function pure

The validation functionality:

    (defn validate-rounds [val] (= (type val) js/Number))

    (defn enforce-number [id value local-copy-of-doc master-copy-of-doc]
      (if (keyword-identical? (first id) :rounds)
        (if (validate-rounds value)
          (assoc @master-copy-of-doc :rounds value)
          @master-copy-of-doc)
        nil))
        
The `bind-fields` function in the `reagent-forms` library binds a form to a map atom using the `id`s defined in the form. The third argument to `bind-fields` is an optional function to add custom behavior to handling change events in the form. That function takes an `id` argument (the `id` of the form element that changed), a `value` argument representing the new value after the change, and a `document` argument containing a map atom with the changed form. I used a closure to include a version of the as-is (pre-change-event) document so that the validator function could be pure. In other words, the only place the document atom is mutated is by `bind-fields`; I did not want side effects in the `enforce-number` function.

### Code listing

Here is the final version of `core.cljs`.

    (ns prisoner.core
      (:require [reagent.core :as reagent :refer [atom]]
                [reagent.session :as session]
                [prisoner.game :as g]
                [prisoner.player :as p]
                [reagent-forms.core :refer [bind-fields]]
                [secretary.core :as secretary :include-macros true]
                [accountant.core :as accountant]))
    
    ;; -------------------------
    ;; State
    
    (def doc (atom {:rounds 500
                    :results nil}))
    
    ;; -------------------------
    ;; Validation
    
    (defn validate-rounds [val] (= (type val) js/Number))
    
    (defn enforce-number [id value local-copy-of-doc master-copy-of-doc]
      (if (keyword-identical? (first id) :rounds)
        (if (validate-rounds value)
          (assoc @master-copy-of-doc :rounds value)
          @master-copy-of-doc)
        nil))
    
    ;; -------------------------
    ;; Components and functions
    
    (def my-form
      [:div.row
      [:div.col-sm-8.col-md-offset-2
        [:div.form-group
        [:label "Player 1 strategy"]
        [:select.form-control {:field :list :id :p1}
          (for [strategy (keys p/players)]
            [:option {:key strategy} (-> p/players strategy :name)])]]
        [:div.form-group
        [:label "Player 2 strategy"]
        [:select.form-control {:field :list :id :p2}
          (for [strategy (keys p/players)]
            [:option {:key strategy} (-> p/players strategy :name)])]]
        [:div.form-group
        [:label "Number of rounds"]
        [:input.form-control
          {:field :numeric
          :type :number
          :id :rounds}]]]])
    
    (defn run-simulation [d]
      (assoc @d :results
            (g/get-score
              (g/run-series
              (-> @d :p1 p/players :fn)
              (-> @d :p2 p/players :fn)
              (:rounds @d)))))
    
    ;; -------------------------
    ;; Views
    
    (defn home-page []
      [:div.container-fluid
      [:div.row
        [:div.col-sm-8.col-md-offset-2
        [:h1 "Iterated Prisoner’s Dilemma Simulator"]]]
      [bind-fields
        my-form
        doc
        (fn [id value local-copy-of-doc]
          (enforce-number id value local-copy-of-doc doc))] ; Keep helper function pure
      [:div.row
        [:div.col-sm-8.col-md-offset-2
        [:button.btn.btn-primary
          {:on-click (fn []
                      (reset! doc (run-simulation doc)))} ; Keep helper function pure
          "Submit"]]]
      [:div.col-sm-8.col-md-offset-2
        (let [results (:results @doc)
              p1-score (first (:results @doc))
              p2-score (second (:results @doc))]
          (if results
            [:div
            [:hr]
            [:h3 "Results"]
            [:table.table-condensed.table-responsive
              [:tr
              [:td [:strong "Player 1"]]
              [:td p1-score]
              (if (< p1-score p2-score) [:td [:strong "Winner"]])]
              [:tr
              [:td [:strong "Player 2"]]
              [:td p2-score]
              (if (< p2-score p1-score) [:td [:strong "Winner"]])]]]))]])
    
    (defn current-page []
      [:div [(session/get :current-page)]])
    
    ;; -------------------------
    ;; Routes
    
    (secretary/defroute "/" []
      (session/put! :current-page #'home-page))
    
    ;; -------------------------
    ;; Initialize app
    
    (defn init! []
      (reagent/render [home-page] (.getElementById js/document "app")))
    
    (enable-console-print!)
